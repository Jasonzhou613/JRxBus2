apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.ttsea.jrxbus2.sample"
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 103
        versionName "1.0.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    //配置keystore签名
    signingConfigs {
        debug {
            storeFile file("keystore/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        setAppName("RxBus2", "MMdd");
    }
}

dependencies {
    compile project(':jrxbus2')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.0.0'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

//设置打包后app的名称；prefix：表示app前缀，dateFormat：表示时间的格式
def setAppName(String prefix, String dateFormat) {
    //自定义编译后的名称
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                String fileName = prefix;
                if (variant.buildType.name != null && !variant.buildType.name.equals("")) {
                    fileName = fileName + "-" + variant.buildType.name;
                }
                if (variant.flavorName != null && !variant.flavorName.equals("")) {
                    fileName = fileName + "-" + variant.flavorName;
                }

                fileName = fileName + "-v" + variant.versionName
                if (!variant.isSigningReady()) {
                    fileName = fileName + "-unsigned";
                } else {
                    fileName = fileName + "-signed";
                }

                String date = new Date().format(dateFormat);
                fileName = fileName + "-" + date + ".apk";

                def apkFile = new File(output.outputFile.getParent(), fileName)
                output.outputFile = apkFile
            }
        }
    }
}